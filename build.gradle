import org.apache.tools.ant.filters.ReplaceTokens
plugins {
  id "com.github.hierynomus.license" version "0.14.0"
  id "com.xebialabs.xldp" version "1.0.5"
  id "com.xebialabs.xl.docker" version "1.1.0"
  id 'nebula.release' version '11.0.0'
}

xlDocker {
  compileImage = 'xebialabs/xld_dev_compile'
  compileVersion = 'v7.1.0.3'
  runImage = 'xebialabs/xld_dev_run'
  runVersion = 'v7.1.0.1'
  runPortMapping = '14516:4516'
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://dist.xebialabs.com/public/maven2'
  }
}


dependencies {
  // Place rest-assured before JUnit dependency to make sure correct Hamcrest is used
  testCompile 'io.rest-assured:rest-assured:3.2.0'
  testCompile "junit:junit:4.11"
  testCompile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
  testCompile 'org.assertj:assertj-core:3.6.2'
  testCompile "org.testcontainers:testcontainers:1.11.3"
  testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.8.0-beta2'
  testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'

  compile 'com.google.guava:guava:23.0'
}


version = "1.0.1"

// sourceCompatibility = 1.7
// targetCompatibility = 1.7

processResources.configure {
  filesNotMatching('**/*.png') {
    filter ReplaceTokens, tokens: [
      'project.version': version.toString(),
      'project.name'   : rootProject.name
    ]
  }
}

license {
  header rootProject.file('License.md')
  strictCheck true
  ext.year = Calendar.getInstance().get(Calendar.YEAR)
  ext.name = 'XEBIALABS'
  exclude '**/Dockerfile'
  exclude '**/*.json'
}
